plugins {
    id 'io.nextflow.nextflow-plugin' version '1.0.0-beta.10'
    id 'java'
    id 'groovy'
}

ext {
    nextflowVersion = '24.10.0'
}
version = '0.1.0'

nextflowPlugin {
    nextflowVersion = '24.10.0'

    provider = 'CUBIC-Curie'
    className = 'nextflow.executor.slurm_no_memory.SlurmNoMemoryPlugin'
    useDefaultDependencies = false
    extensionPoints = [
        'nextflow.executor.slurm_no_memory.SlurmNoMemoryExecutor',
    ]


}


sourceSets {
    main.java.srcDirs = []
    main.groovy.srcDirs = ['src/main/groovy']
    main.resources.srcDirs = ['src/resources']
    test.groovy.srcDirs = ['src/test/groovy']
    test.java.srcDirs = []
    test.resources.srcDirs = ['src/testResources']
}

configurations {
    // Exclude slf4j-api to avoid conflicts with Nextflow's logging
    runtimeClasspath.exclude group: 'org.slf4j', module: 'slf4j-api'
}


dependencies {
    compileOnly "io.nextflow:nextflow:$nextflowVersion"
    compileOnly 'org.slf4j:slf4j-api:2.0.17'
    compileOnly 'org.pf4j:pf4j:3.12.0'
    
    testImplementation "org.apache.groovy:groovy:4.0.26"
    testImplementation "org.apache.groovy:groovy-nio:4.0.26"
    testImplementation "io.nextflow:nextflow:$nextflowVersion"
    testImplementation "org.apache.groovy:groovy-test:4.0.26"
    testImplementation "cglib:cglib-nodep:3.3.0"
    testImplementation "org.objenesis:objenesis:3.1"
    testImplementation "org.spockframework:spock-core:2.3-groovy-4.0"
    testImplementation "org.spockframework:spock-junit4:2.3-groovy-4.0"
    testImplementation "com.google.jimfs:jimfs:1.1"
    
    testImplementation(testFixtures("io.nextflow:nextflow:$nextflowVersion"))
    testImplementation(testFixtures("io.nextflow:nf-commons:$nextflowVersion"))
}

test {
    useJUnitPlatform()
    testLogging {
        // Affiche les événements (tests passés/échoués/ignorés)
        events "passed", "skipped", "failed", "standard_out"
        // Affiche la sortie standard (println)
        showStandardStreams = true
        // Niveau de détail
        exceptionFormat "full"
    }
    jvmArgs([
        '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
        '--add-opens', 'java.base/java.security=ALL-UNNAMED'
    ])
}